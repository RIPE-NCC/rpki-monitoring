import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'io.freefair.lombok' version '6.4.1'
    id 'org.springframework.boot' version '2.6.6'
    id 'com.palantir.git-version' version '0.14.0'
    id 'jacoco'
    id 'org.sonarqube' version '3.3'
    id 'com.google.cloud.tools.jib' version '3.2.1'
}

apply plugin: 'java'
apply plugin: 'io.spring.dependency-management'

group = 'net.ripe.rpki'
version = '0.6.3-SNAPSHOT'

ext['spring-framework.version'] = '5.3.18'

repositories {
    mavenCentral()
    maven {
        url = uri('https://nexus.ripe.net/nexus/content/repositories/releases/')
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-quartz'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // Metrics
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'

    implementation 'net.ripe.rpki:rpki-commons:1.29'
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.1.3'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation('org.assertj:assertj-core:3.22.0')
}

// Replace the application version in resources. But not in test resources.
processResources {
    filter(ReplaceTokens, tokens:[
        gitVersion: gitVersion(),
    ])
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:all'
                         << '-Xlint:-processing'
                         << '-Xlint:-serial'
                         << '-Werror'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

tasks['sonarqube'].dependsOn test

jacocoTestReport {
    reports {
        xml.required = true
    }
}

jib {
    from {
        image = 'docker-registry.ripe.net/swe/gitlab-ci/openjdk-rsync:jre-17'
    }
    to {
        image = 'docker-registry.ripe.net/rpki/rpki-monitoring'
    }
    // JVM memory is set from the environment - might be overridden in the
    // container configuration
    container.environment = [
        JAVA_TOOL_OPTIONS: '-Xms3072m -Xmx3072m'
    ]
}
