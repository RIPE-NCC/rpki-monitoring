import org.apache.tools.ant.filters.ReplaceTokens

plugins {
        id 'org.springframework.boot' version '2.4.3'
        id 'io.spring.dependency-management' version '1.0.9.RELEASE'

        id 'java'
        id 'jacoco'

        id "org.sonarqube" version "3.0"

        // Enable Lombok project (https://projectlombok.org/features/all)
        id "io.freefair.lombok" version "5.3.0"
        // jib: Thinner Java docker images
        id "com.google.cloud.tools.jib" version "2.8.0"
        // git version
        id "com.palantir.git-version" version "0.12.3"
}

jib {
        from {
                image = "docker-registry.ripe.net/swe/gitlab-ci/adoptopenjdk-rsync:jre-11"
        }
        to {
                image = "docker-registry.ripe.net/rpki/rpki-monitoring"
        }
}

// Replace the application version in resources. But not in test resources.
processResources {
        filter(ReplaceTokens, tokens:[
                        gitVersion: gitVersion(),
        ])
}

group = 'net.ripe.rpki'
version = '0.3.4'
sourceCompatibility = '11'


repositories {
        mavenCentral()
        maven {
                url = uri('https://nexus.ripe.net/nexus/content/repositories/releases/')
        }
}

dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-quartz'
        implementation 'org.springframework.boot:spring-boot-starter-web'

        developmentOnly "org.springframework.boot:spring-boot-devtools"

        // Metrics
        implementation "org.springframework.boot:spring-boot-starter-actuator"
        implementation "io.micrometer:micrometer-registry-prometheus"

        implementation 'net.ripe.rpki:rpki-commons:1.21'
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
                exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
}

test {
        useJUnitPlatform()
        finalizedBy jacocoTestReport
}

tasks['sonarqube'].dependsOn test

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

jib.container.environment = [
        JAVA_TOOL_OPTIONS: "-Xms512m -Xmx2048m"
]
