plugins {
    id 'java'
    id 'io.freefair.lombok' version '8.4'
    id 'org.springframework.boot' version '3.1.2'
    id 'com.gorylenko.gradle-git-properties' version '2.4.1'
    id 'jacoco'
    id 'org.sonarqube' version '4.3.0.3225'
}

apply plugin: 'java'
apply plugin: 'io.spring.dependency-management'

group = 'net.ripe.rpki'
version = '0.16.8-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        // READ ACCESS token specific for this repositories maven artifacts. These are public, but unfortunately anonymous
        // access is not possible. Valid until 11-7-2024. NOT A SECRET.
        url = uri('https://ties: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@maven.pkg.github.com/ties/java-rrdp-test-data')
    }
    maven {
        url = uri('https://nexus.ripe.net/nexus/content/repositories/releases/')
    }
}

gitProperties {
    failOnNoGitDirectory = false
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-quartz'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    // for WebClient
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    implementation 'net.ripe.rpki:rpki-commons:1.36'

    implementation 'io.micrometer:micrometer-observation'
    implementation 'io.micrometer:micrometer-registry-otlp'
    implementation 'io.micrometer:micrometer-tracing-bridge-otel'
    implementation 'io.opentelemetry:opentelemetry-exporter-otlp'

    // Metrics
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation("org.assertj:assertj-core:3.24.2")
    testImplementation("com.squareup.okhttp3:mockwebserver:4.11.0")

    // RRDP snapshots for RIPE + APNIC
    testImplementation 'com.github.ties:rrdp-test-data:0.2.0-SNAPSHOT'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(Test).configureEach {
    jvmArgs += "--enable-preview"
}

tasks.withType(JavaExec).configureEach {
    jvmArgs += "--enable-preview"
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs << '--enable-preview'
            << '-Xlint:all'
            << '-Xlint:-processing'
            << '-Xlint:-serial'
            << '-Xlint:-preview'
            << '-Werror'
}

test {
    useJUnitPlatform()
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    finalizedBy jacocoTestReport

    maxHeapSize = '4g'
}

tasks.named('sonarqube').configure {
    it.dependsOn test
}

jacoco {
    // 2023-10-26: Force version 0.8.11 for JDK 21
    toolVersion = "0.8.11"
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}
