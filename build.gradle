import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'io.freefair.lombok' version '6.6.3'
    id 'org.springframework.boot' version '3.0.4'
    id 'com.gorylenko.gradle-git-properties' version '2.4.1'
    id 'jacoco'
    id 'org.sonarqube' version '3.5.0.2730'
    id 'com.google.cloud.tools.jib' version '3.3.1'
}

apply plugin: 'java'
apply plugin: 'io.spring.dependency-management'

group = 'net.ripe.rpki'
version = '0.9.2-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url = uri('https://nexus.ripe.net/nexus/content/repositories/releases/')
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-quartz'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // Metrics
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'

    implementation 'net.ripe.rpki:rpki-commons:1.32'
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.2.1'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation("com.squareup.okhttp3:mockwebserver:4.10.0")
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:all'
                         << '-Xlint:-processing'
                         << '-Xlint:-serial'
                         << '-Werror'
}

test {
    useJUnitPlatform()
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    finalizedBy jacocoTestReport
}

tasks['sonarqube'].dependsOn test

jacocoTestReport {
    reports {
        xml.required = true
    }
}

jib {
    from {
        image = 'docker-registry.ripe.net/swe/gitlab-ci/openjdk-rsync:jre-17'
    }
    to {
        image = 'docker-registry.ripe.net/rpki/rpki-monitoring'
    }
    // JVM memory is set from the environment - might be overridden in the
    // container configuration
    container.environment = [
        JAVA_TOOL_OPTIONS: '-Xms3072m -Xmx3072m'
    ]
}
