import org.apache.tools.ant.filters.ReplaceTokens

plugins {
	id 'org.springframework.boot' version '2.3.0.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'

	id 'java'
	id 'jacoco'

	// Enable Lombok project (https://projectlombok.org/features/all)
	id "io.freefair.lombok" version "5.1.0"
	// jib: Thinner Java docker images
	id "com.google.cloud.tools.jib" version "2.2.0"
	// git version
	id "com.palantir.git-version" version "0.12.3"
}

jib {
	from {
		image = "${project.findProperty('jib.from.image') ?: 'gcr.io/distroless/java:11'}"
		// alternative which includes busybox: gcr.io/distroless/java:11-debug"
	}
	to {
		image = "docker-registry.ripe.net/rpki/rpki-monitoring"
	}
}

// Replace the application version in resources. But not in test resources.
processResources {
	filter(ReplaceTokens, tokens:[
			gitVersion: gitVersion(),
	])
}

group = 'net.ripe.rpki'
version = '0.0.4-SNAPSHOT'
sourceCompatibility = '11'


repositories {
	mavenCentral()
	maven {
		url = uri('https://nexus.ripe.net/nexus/content/repositories/releases/')
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-quartz'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	developmentOnly "org.springframework.boot:spring-boot-devtools"

	// Metrics
	implementation "org.springframework.boot:spring-boot-starter-actuator"
	implementation "io.micrometer:micrometer-registry-prometheus"

	implementation 'net.ripe.rpki:rpki-commons:1.9.0'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}
